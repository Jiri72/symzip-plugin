apply plugin: 'base'

def parentBuildDir= file("../../../../build")
buildDir= file("$parentBuildDir/tmp/test-build")

buildscript {
    repositories {
        flatDir ( dirs: "../../../../build/tmp/libs" )
        mavenCentral()
    }
    dependencies {
        classpath("org.paleozogt:symzip-plugin:")
        classpath 'org.apache.commons:commons-compress:1.9'
        classpath 'commons-io:commons-io:2.4'
    }
}
import org.paleozogt.gradle.zip.*;
import java.nio.file.Files;

ext.testDir    = "foo"
ext.testFile   = "$testDir/bar.txt"
ext.testSymFile= "symfile.txt"
ext.testSymDir = "symdir"
ext.inZipDir   = "test"

task makeTestData() {
    def testDataDir= file("$buildDir/testData")
    outputs.file testDataDir
    doLast {
        file("$testDataDir/$testDir").mkdirs()
        file("$testDataDir/$testFile").write("this is a test")
        ant.symlink(link: file("$testDataDir/$testSymFile"), resource: testFile)
        ant.symlink(link: file("$testDataDir/$testSymDir") , resource: testDir)
    }
}

task runSymZip(type:SymZip, dependsOn:makeTestData) {
    from file(makeTestData.outputs.files.first())
    archiveName "sym-zip.zip"
    into inZipDir
}

task runSymUnzip(type:SymUnzip, dependsOn:runSymZip) {
    from runSymZip.outputs.files.first()
    into file("$buildDir/unzip/sym-unzip")
}

task validateSymUnzip(dependsOn:runSymUnzip) << {
    def testDataDir= makeTestData.outputs.files.first()
    def symUnzipDir= runSymUnzip.outputs.files.first()

    // make sure the file roundtripped
    def file1Contents= file("$testDataDir/$testFile").text
    def file2Contents= file("$symUnzipDir/$inZipDir/$testFile").text
    if (file1Contents != file2Contents) {
        throw new GradleException("file contents doesn't match")
    }

    // make sure the symbolic links roundtripped
    [ testSymFile, testSymDir ].each { f ->
        def filePath = file("$symUnzipDir/$inZipDir/$f").toPath()
        if (!Files.isSymbolicLink(filePath)) {
            throw new GradleException("symbolic link not preserved")
        }
    }
}

task runGradleZip(type:Zip, dependsOn:makeTestData) {
    from file(makeTestData.outputs.files.first())
    archiveName "gradle-zip.zip"
    into inZipDir
}

task runGradleUnzip(type:Copy, dependsOn:runGradleZip) {
    from zipTree(runGradleZip.outputs.files.first())
    into file("$buildDir/unzip/gradle-unzip")
}

task validateGradleUnzip(dependsOn:runGradleUnzip) << {
    def testDataDir= makeTestData.outputs.files.first()
    def symUnzipDir= runGradleUnzip.outputs.files.first()

    // make sure the file roundtripped
    def file1Contents= file("$testDataDir/$testFile").text
    def file2Contents= file("$symUnzipDir/$inZipDir/$testFile").text
    if (file1Contents != file2Contents) {
        throw new GradleException("file contents doesn't match")
    }

    // make sure the symbolic links roundtripped
    [ testSymFile, testSymDir ].each { f ->
        def filePath = file("$symUnzipDir/$inZipDir/$f").toPath()
        if (Files.isSymbolicLink(filePath)) {
            throw new GradleException("symbolic link unexpectedly preserved")
        }
    }
}

task validate(dependsOn:[validateSymUnzip,validateGradleUnzip])

build.dependsOn(validate)
